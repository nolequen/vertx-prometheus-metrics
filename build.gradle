group 'su.nlq'
version '3.5.1'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'jacoco'

sourceCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {
  compile group: 'org.jetbrains', name: 'annotations', version: '15.0'

  compile group: 'io.vertx', name: 'vertx-core', version: '3.6.0-SNAPSHOT'
  compile group: 'io.vertx', name: 'vertx-web', version: '3.6.0-SNAPSHOT'
                                                                                        
  compile group: 'io.prometheus', name: 'simpleclient', version: '0.3.0'
  compile group: 'io.prometheus', name: 'simpleclient_servlet', version: '0.3.0'
  compile group: 'io.prometheus', name: 'simpleclient_common', version: '0.3.0'

  compile group: 'su.nlq', name: 'prometheus-protobuf-servlet', version: '0.2.0'

  testCompile group: 'junit', name: 'junit', version: '4.12'
  testCompile group: 'io.vertx', name: 'vertx-unit', version: '3.5.1'
}

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled false
  }
}

check.dependsOn jacocoTestReport

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { final MavenDeployment deployment -> signing.signPom(deployment) }

      def user = project.hasProperty("ossrhUsername") ? ossrhUsername : ''
      def password = project.hasProperty("ossrhUsername") ? ossrhPassword : ''

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: user, password: password)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: user, password: password)
      }

      pom.project {
        name 'Vert.x Prometheus Metrics'
        description 'Prometheus implementation of the Vert.x Metrics SPI'
        url 'https://github.com/nolequen/vertx-prometheus-metrics'
        licenses {
          license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution 'repo'
          }
          license {
            name "Eclipse Public License - v 1.0"
            url "http://www.eclipse.org/legal/epl-v10.html"
            distribution 'repo'
          }
        }
        scm {
          url 'https://github.com/nolequen/vertx-prometheus-metrics'
          connection 'scm:git:git://github.com/nolequen/vertx-prometheus-metrics.git'
          developerConnection 'scm:git:ssh://github.com:nolequen/vertx-prometheus-metrics.git'
        }
        developers {
          developer {
            name "Nolequen"
            email "nolequen@gmail.com"
            url "http://www.nlq.su/"
          }
        }
      }
    }
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar, javadocJar
}

signing {
  sign configurations.archives
}